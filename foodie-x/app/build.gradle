plugins {
    id 'com.android.application'
    id 'kotlin-android'
    id 'kotlin-kapt'
    id 'androidx.navigation.safeargs.kotlin'
    id 'dagger.hilt.android.plugin'
}

android {
    compileSdk 30
    buildToolsVersion "30.0.3"

    ext {
        appName = "OroSwiss"
        majorVersion = 0
        minorVersion = 1
        patchVersion = 0
        statusVersion = ""
    }

    defaultConfig {
        applicationId "org.xapps.apps.foodiex"
        minSdk 24
        targetSdk 30

        versionCode majorVersion * 10000 + minorVersion * 100 + patchVersion
        versionName majorVersion + "." + minorVersion + "." + patchVersion + statusVersion

        vectorDrawables.useSupportLibrary true

        multiDexEnabled true
//        dexOptions.incremental true
        dexOptions.preDexLibraries false
        dexOptions.javaMaxHeapSize "4g"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildFeatures {
        dataBinding true
        viewBinding true
    }

    kotlinOptions {
        jvmTarget = '11'
    }

    buildTypes {
        debug {
            minifyEnabled false

            buildConfigField "String" , "SPOONACULAR_API_KEY" , "${SPOONACULAR_API_KEY}"
        }

        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'

            buildConfigField "String" , "SPOONACULAR_API_KEY" , "${SPOONACULAR_API_KEY}"

            android.applicationVariants.all { variant ->
                variant.outputs.all { output ->
                    def currentDate = new Date()
                    def timestamp = currentDate.format("yyyyMMddHHmmss")
                    def fileName = (appName + "_"
                            + majorVersion + "." + minorVersion + "." + patchVersion + statusVersion
                            + "_" + timestamp + ".apk")
                    outputFileName = new File(fileName)
                }
            }
        }
    }
}

dependencies {
    // Custom JARs
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // Kotlin
    implementation "androidx.core:core-ktx:1.6.0"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.5.1"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:1.5.1"

    // Jetpack MultiDex
    implementation "androidx.multidex:multidex:2.0.1"

    // Jetpack Annotations Support
    kapt "androidx.annotation:annotation:1.3.0-alpha01"
    implementation "androidx.legacy:legacy-support-v4:1.0.0"

    // Jetpack UI
    implementation "androidx.constraintlayout:constraintlayout:2.0.4"
    implementation "com.google.android.material:material:1.4.0"
    implementation "androidx.appcompat:appcompat:1.3.1"

    // Jetpack Extensions
    implementation "androidx.activity:activity-ktx:1.2.4"
    implementation "androidx.fragment:fragment-ktx:1.3.6"

    // Jetpack Navigation
    implementation "androidx.navigation:navigation-ui-ktx:2.3.5"
    implementation "androidx.navigation:navigation-fragment-ktx:2.3.5"

    // Jetpack DataStore
    implementation "androidx.datastore:datastore-preferences:1.0.0-rc02"

    // Jetpack Lifecycle
    implementation "androidx.lifecycle:lifecycle-runtime-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-extensions:2.2.0"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1"
    implementation "androidx.lifecycle:lifecycle-viewmodel-savedstate:2.3.1"
    implementation "androidx.lifecycle:lifecycle-livedata-ktx:2.3.1"
    kapt "androidx.lifecycle:lifecycle-compiler:2.3.1"

    // Jetpack Room
    implementation "androidx.room:room-runtime:2.4.0-alpha04"
    implementation "androidx.room:room-ktx:2.4.0-alpha04"
    kapt "androidx.room:room-compiler:2.4.0-alpha04"

    // Jetpack Paging
    implementation "androidx.paging:paging-runtime-ktx:3.0.1"

    // Dagger
    kapt "com.google.dagger:dagger-compiler:2.38.1"
    implementation "com.google.dagger:dagger-android:2.38.1"

    // Hilt
    implementation("com.google.dagger:hilt-android:2.38.1")
    kapt("com.google.dagger:hilt-android-compiler:2.38.1")

    // Retrofit
    implementation "com.squareup.retrofit2:retrofit:2.9.0"
    implementation "com.squareup.retrofit2:converter-moshi:2.9.0"
    implementation "com.squareup.okhttp3:logging-interceptor:4.9.1"
    implementation "com.squareup.moshi:moshi-kotlin:1.12.0"

    // Glide
    implementation "com.github.bumptech.glide:glide:4.12.0"
    annotationProcessor "com.github.bumptech.glide:compiler:4.12.0"

    // MaterialProgressBar
    implementation "me.zhanghai.android.materialprogressbar:library:1.6.1"

    // WhatIf
    implementation "com.github.skydoves:whatif:1.1.0"

    // Permissions
    implementation "com.karumi:dexter:6.2.2"

    // Toasty
    implementation "com.github.GrenderG:Toasty:1.5.0"

    // Logger
    implementation "com.jakewharton.timber:timber:4.7.1"
}